////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обновление версии ИБ"
// Клиентские процедуры и функции интерактивного обновления информационной базы.
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Проверить, есть ли необходимость выполнять обновление информационной базы, и
// если необходимо - выполняется обновление.
// Если обновление не удалось выполнить, то процедура предлагает завершить работу системы.
//
Процедура ВыполнитьОбновлениеИнформационнойБазы() Экспорт
	
	Если НЕ ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().НеобходимоОбновлениеИнформационнойБазы Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Пожалуйста, подождите, выполняется обновление информационной базы...'"));
	ДокументОписаниеОбновлений = Неопределено;
	
	Попытка
		ВыполненныеОбработчикиОбновления = ОбновлениеИнформационнойБазы.ВыполнитьОбновлениеИнформационнойБазы();
	Исключение
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'При обновлении информационной базы возникла ошибка:
			|
			|%1
			|
			|Подробности см. в Журнале регистрации.'"), 
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстСообщенияОбОшибке;
	КонецПопытки;
	
	Состояние(НСтр("ru = 'Обновление информационной базы выполнено успешно.'"));
		
	Если ВыполненныеОбработчикиОбновления <> Неопределено Тогда	
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Если ВыполненныеОбработчикиОбновления <> "" Тогда	
		ОткрытьФорму("ОбщаяФорма.ОписаниеИзмененийСистемы", 
			Новый Структура("ВыполненныеОбработчикиОбновления", ВыполненныеОбработчикиОбновления));
	КонецЕсли;
	
КонецПроцедуры

// Открыть форму ОписаниеИзмененийСистемы,
// если есть непоказанные описания изменения, и пользователь не отключил себе просмотр.
//
Процедура ПоказатьОписаниеИзмененийСистемы() Экспорт
	
	Если НЕ ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента();
	Если НЕ ПараметрыРаботыКлиента.Свойство("ПоказатьОписаниеИзмененийСистемы")
		ИЛИ НЕ ПараметрыРаботыКлиента.ПоказатьОписаниеИзмененийСистемы Тогда
		
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ОписаниеИзмененийСистемы"); 
	
КонецПроцедуры

// Проверить, возможно ли выполнять обновление информационной базы.
//
Функция ВозможноВыполнитьОбновлениеИнформационнойБазы() Экспорт
	
	Сообщение = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаЗаблокированаДляОбновления;
	Результат = ПустаяСтрока(Сообщение);
	Если Не Результат Тогда
		Предупреждение(Сообщение);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
