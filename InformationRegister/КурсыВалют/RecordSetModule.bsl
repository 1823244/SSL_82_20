////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// При записи контролируются курсы подчиненных валют
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() > 0 Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			ЗагрузитьКурсДляПодчиненныхВалют(Запись);
		КонецЦикла;
	Иначе
		УдалитьКурсДляПодчиненныхВалют(Отбор.Валюта.Значение, Отбор.Период);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Находит все зависимые валюты и изменяет их курс
//
Процедура ЗагрузитьКурсДляПодчиненныхВалют(ЗаписьВалютыВладельца)
	
	ТаблицаВалют = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ЗаписьВалютыВладельца.Валюта);
	Для Каждого СтрокаТаблицы Из ТаблицаВалют Цикл
		ЗаписьКурсовВалют = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		
		ЗаписьКурсовВалют.Валюта    = СтрокаТаблицы.Ссылка;
		ЗаписьКурсовВалют.Период    = ЗаписьВалютыВладельца.Период;
		ЗаписьКурсовВалют.Курс      = ЗаписьВалютыВладельца.Курс + ЗаписьВалютыВладельца.Курс * СтрокаТаблицы.Наценка / 100;
		ЗаписьКурсовВалют.Кратность = ЗаписьВалютыВладельца.Кратность;
		
		ЗаписьКурсовВалют.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Очищает курсы зависимых волют
//
Процедура УдалитьКурсДляПодчиненныхВалют(ВалютаВладелец, Период)
	
	ТаблицаВалют = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ВалютаВладелец);
	Для Каждого СтрокаТаблицы Из ТаблицаВалют Цикл
		НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Валюта.Значение      = СтрокаТаблицы.Ссылка;
		НаборЗаписей.Отбор.Период.Значение      = Период.Значение;
		НаборЗаписей.Отбор.Период.ЗначениеПо    = Период.ЗначениеПО;
		НаборЗаписей.Отбор.Период.ЗначениеС     = Период.ЗначениеС;
		НаборЗаписей.Отбор.Период.ВидСравнения  = Период.ВидСравнения;
		НаборЗаписей.Отбор.Валюта.Использование = Истина;
		НаборЗаписей.Отбор.Период.Использование = Истина;
		
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры
